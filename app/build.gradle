plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id 'com.github.ben-manes.versions' version '0.45.0'
}

android {
    namespace 'com.hellbrandsdigital.androidtemplatecompose'
    compileSdk 33

    defaultConfig {
        applicationId "com.hellbrandsdigital.androidtemplatecompose"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.hellbrandsdigital.androidtemplatecompose.HiltTestRunner"

        vectorDrawables {
            useSupportLibrary true
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            crunchPngs true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            ext.enableCrashlytics = false
            testCoverageEnabled = true
            debuggable = true
        }
    }

    compileOptions {
        sourceCompatibility "$java_version"
        targetCompatibility "$java_version"
    }

    kotlinOptions {
        jvmTarget = "$jvm_target"
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "$kotlin_compiler_ext_version"
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                it.useJUnitPlatform()
            }
        }
    }
}


// Allow references to generated code
kapt {
    correctErrorTypes true
}

dependencies {
    // Other Modules
    implementation project(path: ':app:features:startScreen')
    implementation project(path: ':app:features:homeScreen')
    implementation project(path: ':app:navigation')
    implementation project(path: ':app:utils')

    def composeBom = platform("androidx.compose:compose-bom:$compose_bom_version")
    implementation(composeBom)
    androidTestImplementation(composeBom)

    implementation libs.bundles.main
    debugImplementation libs.bundles.composeDebug
    kapt libs.bundles.kapt
    testImplementation libs.bundles.test
    androidTestImplementation libs.bundles.androidTest
}
